plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "com.squareup.sqldelight"
    id "androidx.navigation.safeargs.kotlin"
    id "com.google.dagger.hilt.android"
    id "com.google.devtools.ksp"
    id "com.diffplug.spotless"
}

android {
    namespace "com.nikola.jakshic.dagger"
    compileSdk 33
    defaultConfig {
        applicationId "com.nikola.jakshic.dagger"
        minSdk 24
        targetSdk 33
        versionCode 31
        versionName "1.3.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    buildFeatures {
        buildConfig true
        viewBinding true
    }
    compileOptions {
        sourceCompatibility "$jvmTargetVersion"
        targetCompatibility "$jvmTargetVersion"
    }
    kotlinOptions {
        jvmTarget = "$jvmTargetVersion"
    }
}

spotless {
    kotlin {
        target "src/**/*.kt"
        ktlint "0.49.1"
        trimTrailingWhitespace()
        endWithNewline()
    }
}

sqldelight {
    Database {
        sourceFolders = ["java"]
        dialect = "sqlite:3.18"
        schemaOutputDirectory = file("src/debug/java/com/nikola/jakshic/dagger/common/sqldelight")
        // if true - generateSqlDelightInterface fails, verify migrations manually with verifyDebugDatabaseMigration
        verifyMigrations = false
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.activity:activity-ktx:1.7.2"
    implementation "androidx.fragment:fragment-ktx:1.6.0"
    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.google.android.material:material:1.9.0"
    // Networking
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    def moshiVersion = "1.15.0"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    implementation platform("com.squareup.okhttp3:okhttp-bom:4.11.0")
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"
    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"
    // ImageLoading
    implementation "io.coil-kt:coil:2.4.0"
    // Dependency Injection
    implementation "com.google.dagger:hilt-android:$daggerVersion"
    kapt "com.google.dagger:hilt-android-compiler:$daggerVersion"
    // Lifecycle
    def lifecycleVersion = "2.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycleVersion"
    // SQLDelight
    implementation "com.squareup.sqldelight:android-driver:$sqlDelightVersion"
    implementation "com.squareup.sqldelight:coroutines-extensions-jvm:$sqlDelightVersion"
    // Paging
    implementation "androidx.paging:paging-runtime:2.0.0"
    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    // SwipeRefreshLayout
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    // SpiderChart
    implementation "com.github.nikolajakshic:spiderchart:1.0.2"
    // Timber
    implementation "com.jakewharton.timber:timber:5.0.1"
    // LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.10"
    // Test
    kspTest "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:runner:1.5.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0"
}
